# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/notification/distributed_notification_service/notification.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//build/config/components/ets_frontend/ets2abc_config.gni")

taihe_generated_file_path = "${taihe_file_path}/out/${subsystem_name}/${component_name}"

copy_taihe_idl("copy_reminder_agent_manager") {
  sources = [
    "idl/ohos.reminderAgentManager.manager.taihe",
    "idl/ohos.reminderAgentManager.taihe",
  ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path}"
  deps = [ ":copy_reminder_agent_manager" ]
  outputs = [
    "${taihe_generated_file_path}/src/ohos.reminderAgentManager.manager.ani.cpp",
    "${taihe_generated_file_path}/src/ohos.reminderAgentManager.manager.abi.c",
  ]
}

taihe_shared_library("reminder_agent_manager_ani") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  taihe_generated_file_path = "${taihe_generated_file_path}"
  subsystem_name = "${subsystem_name}"
  part_name = "${component_name}"

  include_dirs = [
    "include",
  ]
  sources = get_target_outputs(":run_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.reminderAgentManager.impl.cpp",
    "src/reminder_ani_common.cpp",
  ]

  deps = [
    ":run_taihe",
    "${frameworks_module_reminder_path}:reminder_innerkits",
    "${frameworks_module_ans_path}:ans_innerkits",
    "${frameworks_path}/ets/ani:notification_manager_ani",
  ]

  external_deps = [
    "ability_base:zuri",
    "ability_runtime:ani_common",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]
}

generate_static_abc("reminder_agent_manager_abc") {
  base_url = "${taihe_generated_file_path}"
  files = [ "${taihe_generated_file_path}/@ohos.reminderAgentManager.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/reminder_agent_manager_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("reminder_agent_manager_etc") {
  source = "${target_out_dir}/reminder_agent_manager_abc.abc"
  module_install_dir = "framework"
  part_name = "${component_name}"
  subsystem_name = "${subsystem_name}"
  deps = [
    ":reminder_agent_manager_abc",
  ]
}

group("reminder_agent_manager_taihe") {
  deps = [
    ":reminder_agent_manager_ani",
    ":reminder_agent_manager_etc",
  ]
}