# Copyright (c) 2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/notification/distributed_notification_service/notification.gni")
import("//build/ohos.gni")
import("//build/test.gni")

module_output_path = "${component_name}/distributed_notification_service/unittest"
infra_test_path = "/${services_path}/infrastructure/test"

template("advancednotificationservice_infra_test_target") {
  assert(defined(invoker.sources_value), "sources_value must be defined")

  ohos_unittest(target_name) {
    module_out_path = module_output_path
    sources = []
    sources += invoker.sources_value
    include_dirs = [
      ".",
      "${infra_test_path}/unittest/mock/include",
      "/${services_path}/infrastructure/interfaces",
      "/${services_path}/infrastructure/interfaces/adapter",
      "/${services_path}/infrastructure/external_adapter/bms/include",
      "/${services_path}/infrastructure/external_adapter/account/include",
    ]

    defines = []
    deps = [ 
      "${frameworks_module_ans_path}:ans_innerkits",
      "${services_path}/infrastructure:ans_base",
    ]

    if (defined(invoker.defines_value)) {
      defines += invoker.defines_value
    }

    if (defined(invoker.deps_value)) {
      deps += invoker.deps_value
    }

    if (defined(invoker.common_deps_value)) {
      deps += invoker.common_deps_value
    }

    external_deps = [
      "bundle_framework:appexecfwk_base",
      "bundle_framework:appexecfwk_core",
      "c_utils:utils",
      "ffrt:libffrt",
      "googletest:gmock_main",
      "hilog:libhilog",
      "os_account:os_account_innerkits",
      "samgr:samgr_proxy",
    ]

    if (defined(invoker.external_deps)) {
      deps += invoker.external_deps
    }

    if (distributed_notification_supported) {
      defines += [ "DISTRIBUTED_NOTIFICATION_SUPPORTED" ]
      deps += [ "${services_path}/distributed:libans_distributed" ]
      include_dirs += [ "${services_path}/distributed/include" ]
    }

    if (notification_smart_reminder_supported) {
      defines += [ "NOTIFICATION_SMART_REMINDER_SUPPORTED" ]
    }

    if (device_usage) {
      external_deps += [ "device_usage_statistics:usagestatsinner" ]
      defines += [ "DEVICE_USAGE_STATISTICS_ENABLE" ]
    }

    if (standby_enable) {
      external_deps += [ "device_standby:standby_innerkits" ]
      defines += [ "DEVICE_STANDBY_ENABLE" ]
    }

    if (player_framework) {
      external_deps += [ "player_framework:media_client" ]
      defines += [ "PLAYER_FRAMEWORK_ENABLE" ]
    }

    if (ans_hitrace_usage) {
      external_deps += [ "hitrace:hitrace_meter" ]
      defines += [ "HITRACE_METER_ENABLE" ]
    }

    if (ans_config_policy_enable) {
      external_deps += [ "config_policy:configpolicy_util" ]
      defines += [ "CONFIG_POLICY_ENABLE" ]
    }

    if (screenlock_mgr_enable) {
      external_deps += [ "screenlock_mgr:screenlock_client" ]
      defines += [ "SCREENLOCK_MGR_ENABLE" ]
    }

    if (distributed_notification_service_feature_additional_control ||
        distributed_notification_service_feature_privileged_message) {
      defines += [ "ENABLE_ANS_EXT_WRAPPER" ]
    }

    if (distributed_notification_service_feature_additional_control) {
      defines += [ "ENABLE_ANS_ADDITIONAL_CONTROL" ]
    }

    if (telephony_cust) {
      defines += [ "ENABLE_ANS_TELEPHONY_CUST_WRAPPER" ]
    }

    if (distributed_notification_service_feature_disable_fa_model) {
      defines += [ "ANS_DISABLE_FA_MODEL" ]
    }

    subsystem_name = "${subsystem_name}"
    part_name = "${component_name}"
  }
}
